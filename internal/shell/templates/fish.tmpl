# zgod shell integration for fish
# type -q zgod; and zgod init fish | source
{{if .ConfigPath}}
set -gx ZGOD_CONFIG "{{.ConfigPath}}"
{{end}}

set -g __zgod_session_id ""
set -g __zgod_command ""

function __zgod_has_command
    type -q zgod
end

function __zgod_init
    if test -z "$__zgod_session_id"
        if test -r /proc/sys/kernel/random/uuid
            set -g __zgod_session_id (cat /proc/sys/kernel/random/uuid)
        else if command -v uuidgen &>/dev/null
            set -g __zgod_session_id (uuidgen)
        else
            set -g __zgod_session_id (echo "$$-"(date +%s))
        end
    end
end

function __zgod_preexec --on-event fish_preexec
    __zgod_init
    set -g __zgod_command "$argv"
end

function __zgod_postexec --on-event fish_postexec
    set -l exit_code $status
    if test -z "$__zgod_command"
        return
    end

    if not __zgod_has_command
        set -g __zgod_command ""
        return
    end

    # Fish provides $CMD_DURATION in ms - use it to compute start time
    set -l now_ms (math (date +%s) x 1000)
    set -l start_ms (math "$now_ms - $CMD_DURATION")

    zgod record \
        --ts "$start_ms" \
        --duration "$CMD_DURATION" \
        --exit-code "$exit_code" \
        --command "$__zgod_command" \
        --directory "$PWD" \
        --session "$__zgod_session_id" &

    set -g __zgod_command ""
end

function __zgod_search
    if not __zgod_has_command
        return
    end
    set -l selected (zgod search --height 15 --query (commandline) </dev/tty)
    set -l rc $status
    if test $rc -eq 2; and test -n "$selected"
        commandline -r "$selected"
        commandline -f execute
    else if test $rc -eq 0; and test -n "$selected"
        commandline -r "$selected"
    end
    commandline -f repaint
end

bind \cr __zgod_search
